<?xml version="1.0" encoding="utf-8"?>

<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml"
                        creationComplete="initializeApplication()"
                        showStatusBar="false"
                        paddingBottom="0"
                        paddingLeft="0"
                        paddingRight="0"
                        paddingTop="0"
        >
    <mx:Script>
		<![CDATA[
        import light.book.exif.ExifComponent;
        import light.book.exif.ExifFault;
        import light.book.exif.ExifResult;
        import light.book.exif.MetaInfo;

        import logging.ConsoleTarget;
        import logging.TextTarget;

        import mx.collections.ArrayCollection;
        import mx.logging.ILogger;
        import mx.logging.Log;

        private const log:ILogger = Log.getLogger("ExifExtensionDemo");

        [Bindable]
        private var infos:ArrayCollection = new ArrayCollection();
        private var browser:File = new File();

        private var extension:ExifComponent;

        private function initializeApplication():void
        {
            Log.addTarget(new ConsoleTarget());
            Log.addTarget(new TextTarget(output));

            log.info("started: debug {0}", Capabilities.isDebugger);
            NativeApplication.nativeApplication.addEventListener(Event.EXITING, onExit, false, 0, true);

            extension = new ExifComponent("ExifExtensionDemo");
            extension.addEventListener(ExifResult.RESULT, onResult, false, 0, true);
            extension.addEventListener(ExifFault.FAULT, onError, false, 0, true);

            browser.addEventListener(Event.SELECT, onDirectorySelected);
        }

        private function onError(event:ExifFault):void
        {
            log.error("Code: {0} returned: {1}", event.code, event.error);
        }

        private function onResult(event:ExifResult):void
        {
            //log.info("Code: {0} returned: {1}", event.code, ObjectUtil.toString(event.result));
            log.info("Code: {0} size: {1}", event.code, event.result.length);
            infos.addAll(new ArrayCollection(event.result));
        }

        private function onExit(event:Event):void
        {
            extension.dispose();
        }

        private function onClick(event:MouseEvent):void
        {
            browser.browseForDirectory("Please select a directory...");
        }

        private function onDirectorySelected(event:Event):void
        {
            infos.removeAll();
            var files:Array = browser.getDirectoryListing().filter(function (element:*, index:int, arr:Array):Boolean
            {
                return !File(element).isDirectory;
            });
            log.info("Files: {0}", files.length);
            var programShortPath:String = extension.getShortPath(exiftool.text);
            if (files.length > 0)
            {
                var fileList:Array = [];
                for each (var file:File in files)
                {
                    fileList.push(file.nativePath);
                }
                var code:int = extension.execute(programShortPath, fileList, "-s", "", 512000, -1);
                
                if(code < 0)
                {
                    log.error("execution failed");
                }
            }
        }

        private function onClick2(event:MouseEvent):void
        {
            var path:String = extension.getShortPath(exiftool.text);
            log.info("getShortPath() {0}", path);
        }
        ]]>
	</mx:Script>
    <mx:VBox height="100%" width="100%">
        <mx:HDividedBox height="100%" width="100%">
            <mx:VDividedBox height="100%" width="30%">
                <mx:TextArea id="output" width="100%" height="100%" fontFamily="Courier new" fontSize="12" editable="false"/>
                <mx:VBox width="100%">
                    <mx:TextInput id="exiftool" width="100%" text="c:\Dev\light-book\trunk\ExifComponent\exiftool.exe"/>
                    <mx:TextInput id="params" width="100%" text="c:\Dev\light-book\trunk\ExifComponent\test.fb2"/>
                </mx:VBox>
            </mx:VDividedBox>
            <mx:VBox height="100%" width="70%">
                <mx:DataGrid dataProvider="{infos}" width="100%" height="100%">
                    <mx:columns>
                        <mx:DataGridColumn headerText="File" dataField="FileName"/>
                        <mx:DataGridColumn headerText="File Type" dataField="FileType"/>
                        <mx:DataGridColumn headerText="MIME" dataField="MIMEType"/>
                        <mx:DataGridColumn headerText="File Size" dataField="FileSize"/>
                        <mx:DataGridColumn headerText="File Permissions" dataField="FilePermissions"/>
                        <mx:DataGridColumn headerText="Error" dataField="Error"/>
                        <mx:DataGridColumn headerText="Warning" dataField="Warning"/>
                    </mx:columns>
                </mx:DataGrid>
            </mx:VBox>
        </mx:HDividedBox>
        <mx:HBox>
            <mx:Button label="Run ExifTool" click="onClick(event)"/>
            <mx:Button label="Get short path" click="onClick2(event)"/>
        </mx:HBox>
    </mx:VBox>
</mx:WindowedApplication>